/***************************************************************
* Template for multiple objective evolutionary algorithm in NEURON
* This template is part of the configuration.
* This template defines a parameter object
*
*Author: Shaul Druckmann, Hebrew U, October 2007
*
*Modified by: Etay Hay, Hebrew University of Jerusalem, 2011
***************************************************************/

begintemplate ConfigParameter
	objref name, type, parameter_name, np_name_list, np_value_vec, sp_name_list, sp_value_list
	strdef str_1
	public set_name, set_type,  set_parameter_name, set_numerical_parameters, set_string_parameters
	public get_numerical_parameter, get_string_parameter
	public set_min,set_max,get_min,get_max
	public get_name, get_parameter_name

	proc init()	{
		name = new String()
		type = new String()
		parameter_name = new String()
		
		np_name_list = new List()
		np_value_vec = new Vector()
		sp_name_list = new List()
		sp_value_list  = new List()
		
		min = 0
		max = 0
	}
	
	proc set_name()	{name = new String($s1)}
	proc set_type()	{type = new String($s1)}
	proc set_parameter_name()	{parameter_name = new String($s1)}
	proc set_numerical_parameters() {np_name_list = $o1	np_value_vec = $o2}
	proc set_string_parameters() {sp_name_list = $o1	sp_value_list = $o2}
	
	func get_numerical_parameter()	{local i
		if (np_name_list.count == 0)	{print "No numerical parameters set"	return -1}
		for i=0, np_name_list.count-1	{
			if (strcmp($s1,np_name_list.o(i).s)==0)	{return np_value_vec.x[i]}
		}
		print "Parameter ", $s1, " not found"	return -1
	}
	
	obfunc get_string_parameter()	{local i
		if (sp_name_list.count == 0)	{print "No string parameters set"	return new String("")}
		for i=0, sp_name_list.count-1	{
			if (strcmp($s1,sp_name_list.o(i).s)==0)	{return sp_value_list.o(i)}
		}
		print "Parameter ", $s1, " not found"	return new String("")
	}
	
	proc set_min()	{min = $1}
	proc set_max()	{max = $1}
	func get_min()	{return min}
	func get_max()	{return max}
	
	obfunc get_name()	{return new String(name.s)}
	obfunc get_parameter_name()	{return new String(parameter_name.s)}
endtemplate ConfigParameter
